; Disable authentication
znspace "%SYS"
set sc = ##class(Security.Users).UnExpireUserPasswords("*")
set p("PasswordNeverExpires") = 1
for name="Admin","SuperUser","UnknownUser","_SYSTEM" { do ##class(Security.Users).Modify(name,.p) }
for name="Admin","SuperUser","UnknownUser","_SYSTEM" { do ##class(Security.Users).AddRoles(name, "%All") }
kill p

; Create DEMO namespace
znspace "%SYS"
set dir = $system.Util.GetEnviron("INIT_DIR")
do $system.OBJ.Load(dir _ "/Manifest.cls","ck")
do ##class(Demo.Manifest).setup(, 3)

; Create REST service
znspace "DEMO"
do ##class(%REST.API).CreateApplication("Demo", dir _ "/swagger.json",, .newApplication, .internalError)
do $system.OBJ.Load(dir _ "/implementation.cls","ck")
do ##class(Demo.Implementation).CopyMethods("Demo.impl")

; Create web application
znspace "%SYS"
set p("DispatchClass") = "Demo.disp"
set p("InbndWebServicesEnabled") = 1
set p("NameSpace") = "DEMO"
do ##class(Security.Applications).Create("/demo",.p)
kill p

; Unsecure web access
znspace "%SYS"
set p("AutheEnabled") = 64
set statement = ##class(%SQL.Statement).%New()
set sc = statement.%PrepareClassQuery("Security.Applications", "List")
set resultset = statement.%Execute()
while resultset.%Next() { do ##class(Security.Applications).Modify(resultset.%Get("Name"), .p) }
kill p

; Enable Analytics on the DEMO namespace
znspace "%SYS"
set ^SYS("Security", "CSP", "AllowPrefix", "/csp/demo/", "%DeepSee.") = 1

; Prepare to halt container
znspace "%SYS"
do INT^JRNSTOP
kill ^%SYS("JOURNAL")
kill ^SYS("NODE")
halt
